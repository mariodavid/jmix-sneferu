buildscript {
    repositories {
        gradlePluginPortal()
    }
}

group = 'de.diedavids.jmix.sneferu'

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven {
        url 'https://global.repo.jmix.io/repository/public'
        }
    }

    group = this.group
    version = this.version

    def props = new Properties()
    buildFile.withInputStream { props.load(it) }
    def subArchivesBaseName = props.getProperty('archivesBaseName')
    def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)

    sourceCompatibility = '1.8'

    java {
        withSourcesJar()
    }

    artifacts {
        archives sourcesJar
    }

    if (it.name != 'jmix-sneferu-tests') {
        publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
                    credentials {
                        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                        password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                    }
                }
            }
            publications {
                javaMaven(MavenPublication) {
                    artifactId = archName
                    from components.java
                }
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

}
